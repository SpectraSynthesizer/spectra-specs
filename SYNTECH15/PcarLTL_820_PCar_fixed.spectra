// Initial file contents for AspectLTL aspect file (*.altl)
module PCar
 env {CLEAR, BLOCKED} obstacle ;
 env {CLOSE, CENTERED, FAR, CLOSE_PARKING, PARALLEL_PARKING ,PERPENDICULAR} sideSense;
 env {NA,PARALLEL} parkResult;
 //ext colorSense: {BLACK, GREEN, RED};
 
// new emgButton: boolean;
 sys {FWD, BWD, STOP,BLOCKING_BWD, BLOCKING_FWD, EXIT_FWD} throttle ;
 sys {RIGHT,LEFT, CENTER, EASY_RIGHT, EASY_LEFT} steer ;
 sys {START,STOP,CHECK} parkCommand ;
 sys {SEEKING,MEASURING,PARKING_0,PARKING_1,PARKING_2,PARKED, EXIT_0,EXIT_1, EXIT_2} spec_policy;


//initial state - NOT parking & currently seeking for a parking space
guarantee
( spec_policy=SEEKING );

-----------
-- GOALS --
-----------
//Goal State - be at a parked state.
guarantee
  GF (spec_policy=PARKED);
 
 
 
 guarantee
 -------------------------
 -- ALLOWED TRANSITIONS --
 -------------------------
 G((spec_policy = SEEKING)-> (next(spec_policy)=SEEKING| next(spec_policy) = MEASURING| next(spec_policy) = PARKING_0));
 guarantee
 G ((spec_policy = SEEKING) -> ((throttle = FWD | throttle = STOP | throttle = BWD)));
 
 guarantee
 G((spec_policy = MEASURING)-> (next(spec_policy)=SEEKING| next(spec_policy) = MEASURING| next(spec_policy) = PARKING_0));
  guarantee
 G ((spec_policy = MEASURING) -> (throttle != BLOCKING_BWD & parkCommand != START & (steer = CENTER | steer = EASY_LEFT | steer = EASY_RIGHT)));
 
 
 guarantee
 G((spec_policy = PARKING_0)-> (next(spec_policy)=(PARKING_1)));
 guarantee
 G((spec_policy = PARKING_1)-> (next(spec_policy)=(PARKING_2)));
 guarantee
 G((spec_policy = PARKING_2)-> (next(spec_policy)=(PARKED)));
 guarantee
 G((spec_policy = PARKED)-> (next(spec_policy)=(PARKED) | next(spec_policy)=(EXIT_0)));
 guarantee
 G((spec_policy = EXIT_0)-> (next(spec_policy)=(EXIT_1)));
 guarantee
 G((spec_policy = EXIT_1)-> (next(spec_policy)=(EXIT_2)));
 guarantee
 G((spec_policy = EXIT_2)-> (next(spec_policy)=(SEEKING)));
 

------------------
-- SAFTEY SPECS --
------------------

guarantee
 G (((obstacle = CLEAR) & (spec_policy=SEEKING) & parkResult = NA)->(throttle = FWD));


//if blocked when looking for parking space dont start parking & don't run into obstacles 
guarantee
 G(((obstacle=BLOCKED) & (spec_policy = SEEKING | spec_policy = MEASURING)) ->(next(spec_policy) = SEEKING) & !(throttle = FWD | throttle = BLOCKING_FWD | throttle = BLOCKING_BWD) & !(steer = EASY_RIGHT | steer = EASY_LEFT));
 
//if blocked stop  timing the distance of the parking spot 
guarantee
 G((obstacle=BLOCKED)->(parkCommand=STOP));
 
guarantee
G ((spec_policy = SEEKING & parkResult=NA) -> (throttle != STOP));

------------------------
-- SEEKING ALLIGNMENT --
------------------------
 
 //Allign left
 guarantee
 G((spec_policy=SEEKING & obstacle = CLEAR &  sideSense = CLOSE) -> (steer=EASY_LEFT));
 
  //Allign right
 guarantee
 G((spec_policy=SEEKING & obstacle = CLEAR &  sideSense = FAR) -> (steer=EASY_RIGHT));

 //Allign center
 guarantee
 G((spec_policy=SEEKING & obstacle = CLEAR &  sideSense = CENTERED & (PREV(sideSense = CENTERED))) -> (steer=CENTER));
 
 guarantee
 G((spec_policy = SEEKING & obstacle = CLEAR & PREV(sideSense = FAR) & sideSense = CENTERED) ->
 (steer=EASY_LEFT));
 
 guarantee
 G((spec_policy = SEEKING & obstacle = CLEAR & PREV(sideSense = CLOSE) & sideSense = CENTERED) ->
 (steer=EASY_RIGHT));

//LTLSPECENV
//G ((sideSense = CLOSE) & throttle != STOP -> F(sideSense != CLOSE));
//LTLSPECENV
//G ((sideSense = FAR) & throttle != STOP -> F(sideSense != FAR));

//LTLSPECENV
//G (PREV(sideSense) = FAR & sideSense = CENTERED -> next(sideSense) = CENTERED); 
//LTLSPECENV
//G (PREV(sideSense) = CLOSE & sideSense = CENTERED -> next(sideSense) = CENTERED); 


 
 
  //Allign left
 guarantee
 --------------------------
 -- MEASURING ALLIGNMENT --
 --------------------------
 G((spec_policy=MEASURING & obstacle = CLEAR &  sideSense = CLOSE_PARKING) -> (steer=EASY_LEFT));
 
  //Allign right
 guarantee
 G((spec_policy=MEASURING & obstacle = CLEAR &  sideSense = PERPENDICULAR) -> (steer=EASY_RIGHT));

  //Allign center
 guarantee
 G((spec_policy=MEASURING & obstacle = CLEAR &  sideSense = PARALLEL_PARKING & PREV(sideSense = PARALLEL_PARKING)) -> (steer=CENTER));
 guarantee
 G((spec_policy=MEASURING & obstacle = CLEAR &  sideSense = PARALLEL_PARKING & PREV(sideSense = CLOSE_PARKING)) -> (steer=EASY_RIGHT));
 guarantee
 G((spec_policy=MEASURING & obstacle = CLEAR &  sideSense = PARALLEL_PARKING & PREV(sideSense = PERPENDICULAR)) -> (steer=EASY_LEFT));
 
-----------------------
-- STATE TRANSITIONS --
----------------------- 
 
//Seeking --> Seeking
guarantee
G(((spec_policy = SEEKING) & (sideSense = CLOSE | sideSense = CENTERED | sideSense = FAR) & parkResult=NA)
-> ((next(spec_policy) = spec_policy) & (parkCommand = STOP)));


//Seeking --> Measuring
guarantee
 G (((spec_policy = SEEKING) & !(sideSense = CLOSE | sideSense = CENTERED | sideSense = FAR) & (obstacle = CLEAR))
 -> (next(spec_policy) = MEASURING) & throttle=FWD & parkCommand=START & steer = CENTER);
 

//Measuring --> Seeking 
guarantee
 G (((spec_policy = MEASURING) & (sideSense = CLOSE | sideSense = CENTERED | sideSense = FAR) &(parkResult =NA))
 -> (next(spec_policy) = SEEKING)  & (parkCommand = STOP));

//Measuring --> Measuring

guarantee
 G (((spec_policy = MEASURING) & !(sideSense = CLOSE | sideSense = CENTERED | sideSense = FAR) & (obstacle = CLEAR) &(parkResult =NA))
 -> (next(spec_policy) = spec_policy) & throttle=FWD & parkCommand=CHECK);
 
 //Measuring --> Parking_0
guarantee
 G (((((spec_policy = MEASURING & sideSense = PARALLEL_PARKING)|(spec_policy=SEEKING & PREV(sideSense = PARALLEL_PARKING) & PREV(spec_policy=MEASURING)))) & (obstacle = CLEAR) &(parkResult =PARALLEL))
 -> (next(spec_policy) = PARKING_0) & (throttle = STOP) & (parkCommand = STOP) );

//Parking_0--> Parking_1 
guarantee
 G ((spec_policy = PARKING_0)
 -> (next(spec_policy) = PARKING_1) & (throttle = BLOCKING_FWD) & steer = CENTER & (parkCommand = STOP));

////Parking_1--> Parking_2 
//LTLSPEC
// G ((spec_policy = PARKING_1)// & (sideSense=BLOCKED) & (obstacle = CLEAR) & (parkResult =PARALLEL) 
// -> (next(spec_policy) = PARKING_2) & (throttle = STOP) & steer = CENTER & (parkCommand = STOP));
 
 //Parking_1--> Parking_2 
guarantee
 G ((spec_policy = PARKING_1)
 -> (next(spec_policy) = PARKING_2) & (throttle = BLOCKING_BWD) & steer = RIGHT & (parkCommand = STOP));

 
 //Parking_2--> Parked 
guarantee
 G ((spec_policy = PARKING_2)
 -> (next(spec_policy) = PARKED) & (throttle = BLOCKING_BWD) & steer = LEFT & (parkCommand = STOP));


//Parked--> Parked 
//LTLSPEC
// G (((spec_policy = PARKED))
// -> (next(spec_policy) = spec_policy) & (throttle = STOP) & (steer = CENTER) & (parkCommand = STOP));
 
//Parked-->Exit_0
guarantee
 G (((spec_policy = PARKED))
 -> (next(spec_policy) = EXIT_0) & (throttle = EXIT_FWD) & (steer = LEFT) & (parkCommand = STOP));

//Exit_0-->Exit_1 
guarantee
 G (((spec_policy = EXIT_0))
 -> (next(spec_policy) = EXIT_1) & (throttle = EXIT_FWD) & (steer = RIGHT) & (parkCommand = STOP));

//Exit_1-->Exit_2
guarantee
 G (((spec_policy = EXIT_1))
 -> (next(spec_policy) = EXIT_2) & (steer = CENTER) & (throttle = STOP));
 
//Exit_2-->SEEKING 
guarantee
 G (((spec_policy = EXIT_2))
 -> (next(spec_policy) = SEEKING));

-----------------------------
-- ENIVRONMENT ASSUMPTIONS --
-----------------------------
 
assumption
G ((spec_policy = SEEKING)->next(parkResult = NA));

assumption
  GF ((spec_policy=MEASURING)&(parkResult=PARALLEL)&(obstacle=CLEAR));

assumption
sideSense=CENTERED & parkResult = NA;

assumption
  respondsTo((throttle=BWD),(obstacle=CLEAR));

assumption
 G ((throttle=STOP)->((obstacle=next(obstacle))&(sideSense=next(sideSense))));

//LTLSPECENV
//G F  (((parkResult = PARALLEL) & (obstacle=CLEAR) ) );

//LTLSPECENV
//G ((sideSense = PERPENDICULAR| sideSense = PARALLEL_PARKING) -> F next(sideSense) = sideSense)  ;

 

pattern respondsTo(trigger, response) {
  var boolean responded;
  responded;
  G (next(responded) iff (response or responded and !trigger));
  GF (responded);
}
