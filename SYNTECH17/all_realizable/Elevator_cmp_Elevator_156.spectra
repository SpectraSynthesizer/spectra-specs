import "../patterns/DwyerPatterns.spectra"

module Elevator


type DoorsMotorCmd= {CLOSE, STOP,OPEN};
type LiftMotorCmd= {UP, STOP,DOWN};
type LampCmd= {ON, OFF};
// type VelocityC= {ZERO, SLOW,FAST};



// env variables indicating calls (from floors).
env boolean calls0_c;
env boolean calls1_c;
env boolean calls2_c;
env boolean calls3_c;

env boolean emergency_c;


// Signal indicating that 3,5 and 20 seconds have passed (since the last
// activation of the boolean variable 'countTime_c')
// '1' for 3 seconds, '2' for 5 seconds, '3' for 20 seconds.
env Int(0..3) secondsState_c; 
 
// Those env variables are being connected to the controller, reporting
// current floor\door state.
env Int(0..6) floor_c;
//env DoorsState doors_c;
env Int(0..2) doors_c;

//env boolean shabbatMode;


// System's motors, lamp and timer commands
sys LiftMotorCmd liftM_c;
sys DoorsMotorCmd doorsM_c;
sys LampCmd lamp_c;
sys boolean countTime_c;


// no further comments on the spec, as every gar and asm has a symbolic name.

define 
  goingUp                             :=   (liftM_c = UP);
  goingDown                           :=   (liftM_c = DOWN);
  stayInPlace                         :=   (liftM_c=STOP);
  openingDoors                        :=   (doorsM_c = OPEN);
  closingDoors                        :=   (doorsM_c = CLOSE);
  keepDoorsState                      :=   (doorsM_c = STOP);
  lightIsOn                           :=   (lamp_c = ON);
  doorsAreClosed                      :=   (doors_c = 0);
  doorsAreOpened                      :=   (doors_c = 1);
  doorsAreFullyOpened                 :=   (doors_c = 2);  
  floor0                              :=   (floor_c=0);
  floor1                              :=   (floor_c=2);
  floor2                              :=   (floor_c=4);
  floor3                              :=   (floor_c=6);
  atEdgedFloor                        :=   (floor0 | floor3);
  atFloor                             :=   (floor0|floor1|floor2|floor3); 
  betweenFloors01                     :=   (floor_c=1);
  betweenFloors12                     :=   (floor_c=3);
  betweenFloors23                     :=   (floor_c=5);
  anyCalls                            :=   (calls0_c | calls1_c | calls2_c | calls3_c);
  anyMemCalls                         :=   (memCall0 | memCall1 | memCall2 | memCall3);
  atDesignatedFloor                   :=   (floor0 & memCall0) | (floor1 & memCall1)  | (floor2 & memCall2) | (floor3 & memCall3);
  firstStateAtDesignatedFloor         :=   atDesignatedFloor&doorsAreClosed&!doorsWereFullyOpened&stayInPlace;
  fullyOpenedStateAtDesignatedFloor   :=   atDesignatedFloor&doorsAreFullyOpened;
  lastStateAtDesignatedFloor          :=   atDesignatedFloor&doorsAreClosed&doorsWereFullyOpened;
  
  
  noSeconds                           :=   secondsState_c=0;
  seconds3                            :=   secondsState_c=1;
  seconds5                            :=   secondsState_c=2;
  seconds20                           :=   secondsState_c=3;
  
  onlyDownCalls                       :=   (floor1&(memCall0)&!(memCall1|memCall2|memCall3))|
                                           (floor2&(memCall0|memCall1)&!(memCall2|memCall3));
                                           
  onlyUpCalls                         :=   (floor1&(memCall2|memCall3)&!(memCall0|memCall1))|
                                           (floor2&(memCall3)&!(memCall0|memCall1|memCall2));
  
  atEmergencyFloor                    :=   (atFloor&(memEmergency|(emergency_c&doorsAreClosed)));
  fullyOpenedStateAtEmergencyFloor    :=   (atEmergencyFloor&doorsAreFullyOpened);
  secondHalfOfEmregencyFloor          :=   (atFloor&!atDesignatedFloor&doorsWereFullyOpened);   
  lastStateAtEmergencyFloor           :=   (secondHalfOfEmregencyFloor&doorsAreClosed);                                 

  //emergency := false;


monitor boolean returnTo0
{  
  !returnTo0;
  G (!floor0 & !emergency_c & !memEmergency & ((!anyCalls&!anyMemCalls&seconds20&doorsAreClosed) | returnTo0))<->next (returnTo0); 
}


monitor boolean doorsWereFullyOpened
{
  !doorsWereFullyOpened;
  G (doorsAreFullyOpened->next(doorsWereFullyOpened));
  G (doorsAreClosed->next(!doorsWereFullyOpened));
  G (doorsAreOpened->(next(doorsWereFullyOpened)=doorsWereFullyOpened));
}

monitor boolean memEmergency
{
  !memEmergency;
  G (emergency_c & !memEmergency & doorsAreClosed/* */) -> next (memEmergency);
  G (atFloor&memEmergency&doorsWereFullyOpened&doorsAreClosed) -> next (!memEmergency);
  G (!(emergency_c & !memEmergency & doorsAreClosed/* */) & !(atFloor&memEmergency&doorsWereFullyOpened&doorsAreClosed)) -> (next(memEmergency) = memEmergency);
}

monitor boolean memCall0
{
  !memCall0;
  G (calls0_c & !memCall0 & !emergency_c & (!memEmergency|doorsWereFullyOpened&!floor0)) -> next (memCall0);
  G (memCall0 & ((floor0 & doorsWereFullyOpened & doorsAreClosed)|(fullyOpenedStateAtEmergencyFloor))) -> next(!memCall0);
  G (!(calls0_c & !memCall0 & !emergency_c & (!memEmergency|doorsWereFullyOpened&!floor0)) & !(memCall0 & ((floor0 & doorsWereFullyOpened & doorsAreClosed)|(fullyOpenedStateAtEmergencyFloor)))) -> (next(memCall0) = memCall0);
}

monitor boolean memCall1
{
  !memCall1;
  G (calls1_c & !memCall1 & !emergency_c & (!memEmergency|doorsWereFullyOpened&!floor1)) -> next (memCall1);
  G (memCall1 & ((floor1 & doorsWereFullyOpened & doorsAreClosed)|(fullyOpenedStateAtEmergencyFloor))) -> next(!memCall1);
  G (!(calls1_c & !memCall1 & !emergency_c & (!memEmergency|doorsWereFullyOpened&!floor1)) & !(memCall1 & ((floor1 & doorsWereFullyOpened & doorsAreClosed)|(fullyOpenedStateAtEmergencyFloor)))) -> (next(memCall1) = memCall1);
}

monitor boolean memCall2
{
  !memCall2;
  G (calls2_c & !memCall2 & !emergency_c & (!memEmergency|doorsWereFullyOpened&!floor2)) -> next (memCall2);
  G (memCall2 & ((floor2 & doorsWereFullyOpened & doorsAreClosed)|(fullyOpenedStateAtEmergencyFloor))) -> next(!memCall2);
  G (!(calls2_c & !memCall2 & !emergency_c & (!memEmergency|doorsWereFullyOpened&!floor2)) & !(memCall2 & ((floor2 & doorsWereFullyOpened & doorsAreClosed)|(fullyOpenedStateAtEmergencyFloor)))) -> (next(memCall2) = memCall2);
}

monitor boolean memCall3
{
  !memCall3;
  G (calls3_c & !memCall3 & !emergency_c & (!memEmergency|doorsWereFullyOpened&!floor3)) -> next (memCall3);
  G (memCall3 & ((floor3 & doorsWereFullyOpened & doorsAreClosed)|(fullyOpenedStateAtEmergencyFloor))) -> next(!memCall3);
  G (!(calls3_c & !memCall3 & !emergency_c & (!memEmergency|doorsWereFullyOpened&!floor3)) & !(memCall3 & ((floor3 & doorsWereFullyOpened & doorsAreClosed)|(fullyOpenedStateAtEmergencyFloor)))) -> (next(memCall3) = memCall3);
}



/*
pattern takeCareOfFloor(call,floor,doorsAreClosed)
{
  var boolean memCalltemp;
  
  !memCalltemp;
  
  G (call & !memCalltemp) -> next (memCalltemp);
  G (floor & !doorsAreClosed & next (doorsAreClosed)) -> next (!memCalltemp);
  G (!(call & !memCalltemp) & !(floor & !doorsAreClosed & next (doorsAreClosed))) -> (next (memCalltemp) = memCalltemp);
  
  GF (!memCalltemp);
  
}
*/



//INIT GUARANTEES
gar init_motors:
  (stayInPlace) & (keepDoorsState);


//INIT ASSUMPTIONS
asm startFromFloor0:
  floor0;
  
asm startWithClosedDoors:
  doorsAreClosed;

asm timerHasNotBeenActivatedPriorStart: 
  noSeconds;


//LIGHT GUARANTEES
//gar init_light:
//  !lightIsOn;

//gar lightBlinksWhileMoving: 
//  G !stayInPlace -> (next(lightIsOn) = !lightIsOn);

//gar lightIsOffWhileNotMoving:
//  G (stayInPlace) -> (next(!lightIsOn));
  
//gar
//  G (countTime_c)->next(lightIsOn);
  
//gar
//  G (!countTime_c)->next(!lightIsOn);

gar
  G !lightIsOn;


//Safety Guarantees
gar ifFloor0DontGoDown:
  G (floor0 -> (!goingDown));

gar ifFloor3DontGoUp:
  G (floor3 -> (!goingUp));

gar ifDoorsAreClosedDontClose:
  G (doorsAreClosed -> (!closingDoors));

gar ifDoorsAreFullyOpenedDontOpen:
  G (doorsAreFullyOpened->(!openingDoors));
  
  
  
 
  
//Movement Gars
  
gar dontChangeDirectionWithoutStopping:
  G (goingUp -> next (!goingDown)) & (goingDown -> next (!goingUp));  
  
gar dontStopGoingUpWithoutAReason:
  G goingUp->next(goingUp|atDesignatedFloor|floor3|atEmergencyFloor);
  
gar dontStopGoingDownWithoutAReason:
  G goingDown->next(goingDown|atDesignatedFloor|floor0|atEmergencyFloor);

gar dontMoveWithoutAReason:
  G ((!anyMemCalls & !returnTo0 & !memEmergency) | (returnTo0 & floor0)) -> stayInPlace;

 

//Doors Movement Gars
gar dontMoveDoorsWhileMoving:
  G (!stayInPlace->keepDoorsState);

gar doorsAreClosedWhileMoving:
  G ((!doorsAreClosed)/*|(doorsWereFullyOpened)*/)->(stayInPlace);

gar dontMoveDoorsWithoutAReason:
  G (!atDesignatedFloor&!atEmergencyFloor)->keepDoorsState;

 
gar dontStopOpenningDoorsWithoutAReason:
 G openingDoors->next((stayInPlace&(atDesignatedFloor|atEmergencyFloor)&!doorsAreFullyOpened)->openingDoors);
  
gar dontStopClosingDoorsWithoutAReason:
 G closingDoors->next((stayInPlace&(atDesignatedFloor|atEmergencyFloor)&!doorsAreClosed)->closingDoors);
  
  
  


  
//ENVIROMENT'S DESCRIPTION ASSUMPTIONS

asm keepingTheStateOfTheDoors:
  G (keepDoorsState)->(next(doors_c)=doors_c);

asm moveDoorsOnlyOneStepAtATime:
  G((doors_c=next(doors_c))|(doors_c+1=next(doors_c))|(doors_c-1=next(doors_c)));
  
asm openingDoorsWontDecDoors:
  G openingDoors->((doors_c=next(doors_c))|(doors_c+1=next(doors_c)));

asm closingDoorsWontIntDoors:
  G closingDoors->((doors_c=next(doors_c))|(doors_c-1=next(doors_c)));
 
asm openingDoorsWithoutStoppingCauseIncDoors:
  pRespondsToS(openingDoors&!doorsAreFullyOpened,(next(doors_c)=doors_c+1)|!openingDoors);
  
asm closingDoorsWithoutStoppingCauseDecDoors:
  pRespondsToS(closingDoors&!doorsAreClosed,(next(doors_c)=doors_c-1)|!closingDoors);
  

asm floorChangeCannotHappenWithoutMoving: 
  G (stayInPlace)->(next(floor_c)=floor_c);
    
asm moveOnlyOneFloorAtATime:
  G((floor_c=next(floor_c))|(floor_c+1=next(floor_c))|(floor_c-1=next(floor_c)));
 
asm goingUpWontDecFloor:
  G goingUp->((floor_c=next(floor_c))|(floor_c+1=next(floor_c)));

asm goingDownWontIncFloor:
  G goingDown->((floor_c=next(floor_c))|(floor_c-1=next(floor_c)));
  

       
asm goingUpWithoutStoppingCauseIncFloor:
  pRespondsToS(goingUp&!floor3,(next(floor_c)=floor_c+1)|!goingUp);
  
  
asm goingDownWithoutStoppingCauseDecFloor:
  pRespondsToS(goingDown&!floor0,(next(floor_c)=floor_c-1)|!goingDown);
  




//CLOCK GUARANTEES AND ASSUMPTIONS
    
gar startCountTime:
  G ((!((emergency_c&doorsAreClosed)|memEmergency))&(!(!stayInPlace|anyCalls|anyMemCalls|floor0|returnTo0) | firstStateAtDesignatedFloor&(!returnTo0) | fullyOpenedStateAtDesignatedFloor))->(countTime_c);
  
gar stopCountTime:
  G (((emergency_c&doorsAreClosed)|memEmergency)|(returnTo0)|(!atDesignatedFloor&(floor0|anyCalls|anyMemCalls))|(!stayInPlace)|(lastStateAtDesignatedFloor)|(doorsAreOpened))->(!countTime_c);

asm envDoesntCountSecondsWhenNotRequired:
  G (!countTime_c) -> next(noSeconds);

asm countOnlyOneUnitAtATime:
  G (countTime_c)->((next(secondsState_c)=secondsState_c)|(next(secondsState_c)=secondsState_c+1));
  

asm takesSomeTimeToCount3SecondsFIXDELAY:
  G (PREV(!countTime_c))->next(noSeconds);

asm takesSomeTimeToCount5SecondsFIXDELAY:
  G (PREV(noSeconds)/*&(seconds3)*/)->next(!seconds5);

 
asm canCountSecondsWhenRequired:
    pRespondsToS(countTime_c&(!seconds20),next(secondsState_c)=secondsState_c+1|!countTime_c);

  

//DESIGNATED FLOOR GUARANTEES

//gar ifAtDesignatedFloorDontMove:
//   G atDesignatedFloor->(staying);

gar ifEnteredToDesignatedFloorDontMove:
  G (!atFloor&next(atDesignatedFloor)&(!returnTo0))->next(stayInPlace);

//Used for 2 cases: 1.if standing in a floor and it calls. 2.if standing in a designated floor don't move.
gar atDesignatedFloorAndNotMovingThenDontMove:
  G (atFloor&stayInPlace&next(atDesignatedFloor)/*&!returnTo0*/)->next(stayInPlace);

//Used to make sure that an unfair user won't stuck the elevator at one floor
gar ifStartedToLeaveFloorKeepGoing:
  G (atFloor&!stayInPlace)/*&(floor_c=next(floor_c))*/->next(!stayInPlace|atEmergencyFloor);

gar dontGoUpWhenOnlyCallsFromDown:
  G (onlyDownCalls&PREV(stayInPlace))->(goingDown|atEmergencyFloor);

gar dontGoDownWhenOnlyCallsFromUp:
  G (onlyUpCalls&PREV(stayInPlace))->(goingUp|atEmergencyFloor);


gar AtDesignatedFloorDontOpenDoorsUntill3Seconds:
    //pHolds_afterQuntilR(keepDoorsState,firstStateAtDesignatedFloor,!noSeconds);
    G(firstStateAtDesignatedFloor&(noSeconds)/*&!emergency*/&!atEmergencyFloor)->keepDoorsState;

    
gar AtDesignatedFloorHoldTheDoorsFullyOpenedFor5Seconds:
    //pHolds_afterQuntilR(keepDoorsState,fullyOpenedStateAtDesignatedFloor,!(noSeconds|seconds3));
    G(fullyOpenedStateAtDesignatedFloor&(noSeconds|seconds3)/*&!emergency*/&!memEmergency)->keepDoorsState;




//REACH TO DESIGNATED FLOORS

gar takeCareOfCallsFromFloor0:
  GF(!memCall0);

gar takeCareOfCallsFromFloor1:
  GF(!memCall1);
  
gar takeCareOfCallsFromFloor2:
  GF(!memCall2);
  
gar takeCareOfCallsFromFloor3:
  GF(!memCall3);




//RETURN TO FLOOR0 GUARANTEES AND ASSUMPTIONS

gar returnTo0WhenRequired:
  //pHolds_afterQuntilR(goingDown,returnTo0,floor0);
  G (returnTo0&!floor0)->(goingDown|atEmergencyFloor);



//EMERGENCY GUARANTEES AND ASSUMPIONS

asm noEmergencyInThebeginning:
  !emergency_c;
  
//asm
//  G !emergency;


//gar ifEnteredToEmergencyFloorDontMove: 
//  G (!atFloor&next(atEmergencyFloor)->next(stayInPlace));

//gar atEmergencyFloorAndNotMovingThenDontMove:
//  G (atFloor&stayInPlace&next(atEmergencyFloor))->next(stayInPlace);

gar atEmergencyFloorDontMove:
  G (atEmergencyFloor->stayInPlace);

gar AtEmergencyFloorOpenDoors:
  //pRespondsToS(atEmergencyFloor&!doorsWereFullyOpened,doorsAreFullyOpened);
  G (atEmergencyFloor&!doorsWereFullyOpened&!doorsAreFullyOpened&stayInPlace)->openingDoors;
  
 
